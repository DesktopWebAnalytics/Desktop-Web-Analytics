<?xml version="1.0" encoding="utf-8"?>
<!--
	Desktop Web Analytics
	
	Link http://www.desktop-web-analytics.com
	Link https://github.com/DesktopWebAnalytics
	License http://www.gnu.org/licenses/gpl-3.0-standalone.html GPL v3 or later
	
	$Id: OperatingSystems.mxml 333 2012-04-02 13:23:26Z benoit $
-->
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" >
	<fx:Script>
		<![CDATA[
			import com.dwa.common.reports.Reports;
			
			import mx.charts.events.ChartItemEvent;
			import mx.charts.series.items.PieSeriesItem;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.collections.XMLListCollection;
			import mx.core.FlexGlobals;
			import mx.effects.easing.*;
			
			private var os:XMLListCollection;
			
			private var report:Reports;
			
			public function activeView():void {
				search.text = "";
				dataGridOperatingSystems.dataProvider = null;
				showBusy();
				report = new Reports(FlexGlobals.topLevelApplication.shortLocale);
				
				report.addEventListener(Event.COMPLETE, result);
				report.addEventListener(ErrorEvent.ERROR, error);
				report.getOs(FlexGlobals.topLevelApplication.selectedWebsite, FlexGlobals.topLevelApplication.dateRange);
			}
			private function removeListeners():void{
				report.removeEventListener(Event.COMPLETE, result);
				report.removeEventListener(ErrorEvent.ERROR, error);
				report = null;
			}
			private function error(event:ErrorEvent):void{
				trace("error");
				hideBusy();
				info.visible = info.includeInLayout = true;
				info.toolTip = event.text;
				removeListeners();
			}
			private function result(event:Event):void {
				os = report.resultCollectionRow;
				removeListeners();
				
				hideBusy();
				
				os.sort = new Sort();
				os.sort.fields = [new SortField("nb_visits", true, true, true)];
				os.refresh();
				
				dataGridOperatingSystems.dataProvider = os;
				
				var pie:int = 8;
				
				if(os.length > pie){
					var sumOs:XMLListCollection = new XMLListCollection();
					var sumTotal:int = 0;
					var sum:int = 0;
					var k:int = 0;
					var oper:int;
					
					for (k; k < os.length; k++){
						oper = os[k].nb_visits;
						if(k < pie){
							sumOs.addItem(os[k]);
							sum += oper;
						}
						sumTotal += oper;
					}
					
					var rowOther:XML = new XML("<row><label>" + resourceManager.getString('piwik', 'General_Others') +"</label><nb_visits>" + Number(sumTotal-sum) + "</nb_visits></row>");
					sumOs.addItem(rowOther);
					
					pieChartOs.dataProvider = sumOs;
					
				}else{
					pieChartOs.dataProvider = os;
				}
				pieSeries.nameField = "label";
				pieSeries.field = "nb_visits";
				
				
			}
			
			private function pieChart_itemClick(evt:ChartItemEvent):void {
                var item:PieSeriesItem = evt.hitData.chartItem as PieSeriesItem;
                var degrees:Number = radiansToDegrees(item.startAngle);
                var arr:Array = [];
                /* if (checkBox.selected) {
                    arr[item.index] = 0.2;
                } */
                arr[item.index] = 0.2;
                
                pieSeries.perWedgeExplodeRadius = arr;
                pieSeries.startAngle -= degrees;

                os.refresh();
                
            }

            private function radiansToDegrees(radians:Number):Number {
                return radians * (180 / Math.PI);
            }
            
            private function displayOs(data:Object, field:String, index:Number, percentValue:Number):String {
            	var temp:String = rounding.format(percentValue);
            	return data.label +": " + temp + "%" + "\n" + resourceManager.getString('piwik', 'General_ColumnNbVisits') + ": " + data.nb_visits;
            }
            
            private function showBusy():void{
				info.visible = info.includeInLayout = false;
				progress.visible = progress.includeInLayout = true;
			}
			private function hideBusy():void{
				progress.visible = progress.includeInLayout = false;
			}
			
			private function filterFunction(item:XML):Boolean{
				var reg:RegExp = new RegExp(search.text, "ig");
				
				return reg.test(item.label);
			}
			private function showFilter():void{
				if(search.text != ""){
					os.filterFunction = filterFunction;
				}else{
					os.filterFunction = null;
				}
				os.refresh();
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:NumberFormatter id="rounding" fractionalDigits="1" />
	</fx:Declarations>
	
	<s:VGroup width="100%" height="100%" gap="1">
		<mx:PieChart id="pieChartOs" showDataTips="true" width="100%" height="100%" itemClick="pieChart_itemClick(event)">
			<mx:series>
				<mx:PieSeries id="pieSeries" labelPosition="callout" labelFunction="{displayOs}">
					<mx:showDataEffect>
						<mx:SeriesInterpolate duration="1200" easingFunction="{Elastic.easeOut}" />
					</mx:showDataEffect>
					<mx:filters>
						<mx:DropShadowFilter />
					</mx:filters>
				</mx:PieSeries>
			</mx:series>
		</mx:PieChart>
		
		<s:HGroup width="100%" horizontalAlign="right">
			<s:TextInput id="search" prompt="{resourceManager.getString('piwik', 'General_Search')}" width="200" change="showFilter()" />
		</s:HGroup>
		
		<s:DataGrid id="dataGridOperatingSystems" width="100%" height="50%" sortableColumns="false" >
			<s:columns>
				<s:ArrayList>
					<s:GridColumn dataField="label" headerText="{resourceManager.getString('piwik', 'UserSettings_ColumnOperatingSystem')}" itemRenderer="views.reports.renderers.ItemRender" />
					<s:GridColumn dataField="nb_visits" headerText="{resourceManager.getString('piwik', 'General_ColumnNbVisits')}" />
				</s:ArrayList>
			</s:columns>
		</s:DataGrid>
		
		<s:Group width="100%" bottom="0">
			<mx:ProgressBar id="progress" left="0" indeterminate="true" label="{resourceManager.getString('piwik', 'General_LoadingData')}" />
			<s:HGroup id="info" visible="false" right="0">
				<s:BitmapImage source="@Embed('assets/generalIcons/exclamation.png')" />
				<s:Label text="{resourceManager.getString('piwik', 'General_ErrorRequest')}" />
			</s:HGroup>
		</s:Group>
	</s:VGroup>
</s:Group>